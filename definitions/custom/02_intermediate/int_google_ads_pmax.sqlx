config {
  type: "table",
  schema: dataform.projectConfig.vars.TRANSFORMATIONS_DATASET,
  name: "int_google_ads_pmax"
}

WITH pmax_campaign_base AS (
    SELECT
        c.campaign_id,
        c.customer_id,
        c.campaign_name
    FROM
        ${ref("ads_Campaign_1534842056")} c
    WHERE
        c._DATA_DATE = c._LATEST_DATE
        AND c.customer_id = 1703013237
        AND c.campaign_advertising_channel_type = 'PERFORMANCE_MAX'
),
pmax_assetgroup_base AS (
    SELECT
        REPLACE(ag.asset_group_id, 'assetGroups/', '') AS asset_group_id,
        ag.asset_group_name,
        CAST(REGEXP_EXTRACT(ag.asset_group_campaign, r'campaigns/(\d+)') AS INT64) AS campaign_id,
        REGEXP_EXTRACT(ag.asset_group_final_urls, r'"(.*?)"') AS final_url
    FROM
        ${ref("ads_AssetGroup_1534842056")} ag
    WHERE
        ag._DATA_DATE = ag._LATEST_DATE
),
pmax_stats AS (
    SELECT
        cs.campaign_id,
        cs.customer_id,
        cs.segments_date AS date,
        SUM(cs.metrics_impressions) AS impressions,
        SUM(cs.metrics_clicks) AS clicks,
        SUM(cs.metrics_cost_micros) / 1000000 AS spend
    FROM
        ${ref("ads_CampaignBasicStats_1534842056")} cs
    WHERE
        cs._DATA_DATE = cs._LATEST_DATE
        AND cs.customer_id = 1703013237
    GROUP BY 1, 2, 3
),
pmax_conversion_stats AS (
    WITH exploded AS (
        SELECT
            conv.campaign_id,
            conv.customer_id,
            conv.segments_date AS date,
            LOWER(conv.segments_conversion_action_name) AS name,
            COALESCE(conv.metrics_conversions, 0) AS conversions
        FROM
            ${ref("ads_CampaignConversionStats_1534842056")} conv
        WHERE
            conv._DATA_DATE = conv._LATEST_DATE
            AND conv.customer_id = 1703013237
    )
    SELECT
        campaign_id,
        customer_id,
        date,
        SUM(CASE WHEN name = 'cmb' THEN conversions ELSE 0 END) AS cmb,
        SUM(CASE WHEN name = 'form' THEN conversions ELSE 0 END) AS form,
        0 AS lead_ads,
        SUM(conversions) AS total_conversions
    FROM
        exploded
    GROUP BY 1, 2, 3
),
click_details AS (
    SELECT
        cs.Gclid,
        cs.AdGroupId,
        cs.CampaignId,
        cs.CustomerId,
        cs.Date
    FROM
        ${ref("ads_ClickStats_1534842056")} cs
    WHERE
        cs.CustomerId = 1703013237
        AND cs.Gclid IS NOT NULL
)
SELECT
    'Google Ads' AS Platform,
    cd.Date AS date,
    CAST(pc.campaign_id AS STRING) AS id,
    pc.campaign_name AS campaign,
    pag.asset_group_name AS adset,
    CAST(cd.AdGroupId AS STRING) AS adset_id,
    pag.asset_group_name AS ad,
    CAST(cd.AdGroupId AS STRING) AS ad_id,
    pag.final_url,
    COALESCE(SAFE_DIVIDE(ps.spend, NULLIF(ps.clicks, 0)), 0) AS spend,
    COALESCE(SAFE_DIVIDE(ps.impressions, NULLIF(ps.clicks, 0)), 0) AS impressions,
    COALESCE(SAFE_DIVIDE(ps.clicks, NULLIF(ps.clicks, 0)), 0) AS clicks,
    NULL AS link_clicks,
    COALESCE(SAFE_DIVIDE(pconv.cmb, NULLIF(ps.clicks, 0)), 0) AS cmb,
    COALESCE(SAFE_DIVIDE(pconv.form, NULLIF(ps.clicks, 0)), 0) AS form,
    COALESCE(SAFE_DIVIDE(pconv.lead_ads, NULLIF(ps.clicks, 0)), 0) AS lead_ads,
    COALESCE(SAFE_DIVIDE(pconv.total_conversions, NULLIF(ps.clicks, 0)), 0) AS total_conversions,
    cd.Gclid
FROM
    click_details cd
LEFT JOIN pmax_stats ps
    ON cd.CampaignId = ps.campaign_id
    AND cd.CustomerId = ps.customer_id
    AND cd.Date = ps.date
LEFT JOIN pmax_conversion_stats pconv
    ON cd.CampaignId = pconv.campaign_id
    AND cd.CustomerId = pconv.customer_id
    AND cd.Date = pconv.date
JOIN pmax_campaign_base pc
    ON cd.CampaignId = pc.campaign_id
    AND cd.CustomerId = pc.customer_id
LEFT JOIN pmax_assetgroup_base pag
    ON CAST(cd.AdGroupId AS STRING) = pag.asset_group_id
