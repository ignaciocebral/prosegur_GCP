config {
  type: "table",
  schema: dataform.projectConfig.vars.TRANSFORMATIONS_DATASET,
  name: "int_google_ads_pmax",
  tags: [dataform.projectConfig.vars.LABEL_EXECUTION_OPERATION],
  disabled: true
}

WITH pmax_campaign_base AS (
    SELECT
        c.campaign_id,
        c.customer_id,
        c.campaign_name
    FROM
        ${ref("ads_Campaign_1534842056")} c
    WHERE
        c._DATA_DATE = c._LATEST_DATE
        AND c.customer_id = ${dataform.projectConfig.vars.GOOGLE_ADS_CUSTOMER_ID}
        AND c.campaign_advertising_channel_type = 'PERFORMANCE_MAX'
),
pmax_assetgroup_base AS (
    SELECT
        REPLACE(ag.asset_group_id, 'assetGroups/', '') AS asset_group_id,
        ag.asset_group_name,
        CAST(REGEXP_EXTRACT(ag.asset_group_campaign, r'campaigns/(\d+)') AS INT64) AS campaign_id,
        REGEXP_EXTRACT(ag.asset_group_final_urls, r'"(.*?)"') AS final_url
    FROM
        ${ref("ads_AssetGroup_1534842056")} ag
    WHERE
        ag._DATA_DATE = ag._LATEST_DATE
),
pmax_stats AS (
    SELECT
        cs.campaign_id,
        cs.customer_id,
        cs.segments_date AS date,
        SUM(cs.metrics_impressions) AS impressions,
        SUM(cs.metrics_clicks) AS clicks,
        SUM(cs.metrics_cost_micros) / 1000000 AS spend
    FROM
        ${ref("ads_CampaignBasicStats_1534842056")} cs
    WHERE
        cs._DATA_DATE = cs._LATEST_DATE
        AND cs.customer_id = ${dataform.projectConfig.vars.GOOGLE_ADS_CUSTOMER_ID}
    GROUP BY 1, 2, 3
),
pmax_conversion_stats AS (
    WITH exploded AS (
        SELECT
            conv.campaign_id,
            conv.customer_id,
            conv.segments_date AS date,
            LOWER(conv.segments_conversion_action_name) AS name,
            COALESCE(conv.metrics_conversions, 0) AS conversions
        FROM
            ${ref("ads_CampaignConversionStats_1534842056")} conv
        WHERE
            conv._DATA_DATE = conv._LATEST_DATE
            AND conv.customer_id = ${dataform.projectConfig.vars.GOOGLE_ADS_CUSTOMER_ID}
    )
    SELECT
        campaign_id,
        customer_id,
        date,
        SUM(CASE WHEN name = 'cmb' THEN conversions ELSE 0 END) AS cmb,
        SUM(CASE WHEN name = 'form' THEN conversions ELSE 0 END) AS form,
        0 AS lead_ads,
        SUM(conversions) AS total_conversions
    FROM
        exploded
    GROUP BY 1, 2, 3
),
clicks_with_cost AS (
    SELECT
        cs.click_view_gclid as Gclid,
        cs.campaign_id,
        cs.segments_date as Date,
        SAFE_DIVIDE(s.spend, s.clicks) AS cost_per_click
    FROM
        ${ref("ads_ClickStats_1534842056")} cs
    LEFT JOIN pmax_stats s
        ON cs.campaign_id = s.campaign_id
        AND cs.segments_date = s.date
    WHERE cs.customer_id = ${dataform.projectConfig.vars.GOOGLE_ADS_CUSTOMER_ID}
),
-- Campaign-level metrics: one row per campaign/date (no fanout from clicks)
campaign_metrics AS (
    SELECT
        ps.date,
        ps.campaign_id,
        ps.customer_id,
        pc.campaign_name,
        ps.impressions,
        ps.clicks,
        ps.spend,
        coalesce(pconv.cmb, 0) as cmb,
        coalesce(pconv.form, 0) as form,
        coalesce(pconv.lead_ads, 0) as lead_ads,
        coalesce(pconv.total_conversions, 0) as total_conversions
    FROM pmax_stats ps
    LEFT JOIN pmax_conversion_stats pconv
        ON ps.campaign_id = pconv.campaign_id
        AND ps.customer_id = pconv.customer_id
        AND ps.date = pconv.date
    LEFT JOIN pmax_campaign_base pc
        ON ps.campaign_id = pc.campaign_id
        AND ps.customer_id = pc.customer_id
)
-- Click-level rows: one row per gclid, only cost_per_click at this grain
SELECT
    'Google Ads' AS Platform,
    cwc.Date as date,
    CAST(cm.campaign_id AS STRING) AS id,
    cm.campaign_name AS campaign,
    pag.asset_group_name AS adset,
    CAST(pag.asset_group_id AS STRING) AS adset_id,
    pag.asset_group_name AS ad,
    CAST(pag.asset_group_id AS STRING) AS ad_id,
    pag.final_url,
    cwc.cost_per_click AS spend,
    NULL AS impressions,  -- impressions are campaign-level, not per click
    1 AS clicks,          -- each row is one click
    NULL AS link_clicks,
    NULL AS cmb,
    NULL AS form,
    NULL AS lead_ads,
    NULL AS total_conversions,
    cwc.Gclid
FROM
    clicks_with_cost cwc
LEFT JOIN campaign_metrics cm
    ON cwc.campaign_id = cm.campaign_id
    AND cwc.Date = cm.date
LEFT JOIN pmax_assetgroup_base pag
    ON cwc.campaign_id = pag.campaign_id